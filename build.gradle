plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'signing'
}

group = 'dev.thatsnasu'
version = '0.1.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
    withJavadocJar()
    withSourcesJar()
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			groupId = 'dev.thatsnasu'
			artifactId = 'OpenIRC'
			version = '0.1.0'
			pom {
				name = 'OpenIRC'
				description = 'Open source irc library'
				url = 'https://github.com/thatsnasu/openirc'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/thatsnasu/openirc/blob/main/LICENSE'
					}
				}
				developers {
					developer {
						id = 'thatsnasu'
						name = 'Hauke Motzkus'
						email = 'thatsnasu@bitbite.dev'
						organization = 'bitbite'
						organizationUrl = 'https://bitbite.dev'
					}
				}
				scm {
					connection = 'scm:git:git://gihub.com/thatsnasu/openirc.git'
					url = 'https://github.com/thatsnasu/openirc'
				}
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			credentials {
				//username = ossrhUsername
				//password = ossrhPassword
				username = System.getenv("MAVEN_USERNAME")
				password = System.getenv("MAVEN_PASSWORD")
			}
			def releasesRepositoryURL = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotRepositoryURL = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotRepositoryURL : releasesRepositoryURL
		}
	}
}

signing {
	sign publishing.publications.maven
}

artifacts {
    archives javadocJar, sourcesJar
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
	implementation 'dev.bitbite:OpenLogLib:0.4.1'
	implementation 'dev.bitbite:OpenNetLib:1.1.0'
	implementation 'dev.thatsnasu:Ansi:1.1.1'

    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
